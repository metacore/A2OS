(* ETH Oberon, Copyright 2001 ETH Zuerich Institut fuer Computersysteme, ETH Zentrum, CH-8092 Zuerich.
Refer to the "General ETH Oberon System Source License" contract available at: http://www.oberon.ethz.ch/ *)

MODULE MathL;	(** portable *)
(** AUTHOR "?"; PURPOSE "Math utility module (LONGREAL)"; *)

(* Aos version - requires floating-point instruction support. *)

IMPORT SYSTEM;

CONST
	e* = 2.7182818284590452354D0;
	pi* = 3.14159265358979323846D0;

TYPE
	Value = LONGREAL;

PROCEDURE -Sin(x: Value):Value;
CODE
#IF I386 THEN
	FLD QWORD [ESP]
	FSIN
	#IF ~LEGACY THEN
		FSTP QWORD [ESP]
		MOVSD XMM0, [ESP]
	#END
	POP EAX
	POP EAX
#ELSIF AMD64 THEN
	FLD QWORD [RSP]
	FSIN
	#IF ~LEGACY THEN
		FSTP QWORD [RSP]
		MOVSD XMM0, [RSP]
	#END
	POP RAX
#ELSE
	unimplemented
#END
END Sin;

PROCEDURE -Cos(x: Value):Value;
CODE
#IF I386 THEN
	FLD QWORD [ESP]
	FCOS
	#IF ~LEGACY THEN
		FSTP QWORD [ESP]
		MOVSD XMM0, [ESP]
	#END
	POP EAX
	POP EAX
#ELSIF AMD64 THEN
	FLD QWORD [RSP]
	FCOS
	#IF ~LEGACY THEN
		FSTP QWORD [RSP]
		MOVSD XMM0, [RSP]
	#END
	POP RAX
#ELSE
	unimplemented
#END
END Cos;

PROCEDURE -Arctan(x: Value):Value;
CODE
#IF I386 THEN
	FLD QWORD [ESP]
	FLD1
	FPATAN
	#IF ~LEGACY THEN
		FSTP QWORD [ESP]
		MOVSD XMM0, [ESP]
	#END
	POP EAX
	POP EAX
#ELSIF AMD64 THEN
	FLD QWORD [RSP]
	FLD1
	FPATAN
	#IF ~LEGACY THEN
		FSTP QWORD [RSP]
		MOVSD XMM0, [RSP]
	#END
	POP RAX
#ELSE
	unimplemented
#END
END Arctan;

PROCEDURE -Sqrt(x: Value):Value;
CODE
#IF I386 THEN
	FLD QWORD [ESP]
	FSQRT
	#IF ~LEGACY THEN
		FSTP QWORD [ESP]
		MOVSD XMM0, [ESP]
	#END
	POP EAX
	POP EAX
#ELSIF AMD64 THEN
	FLD QWORD [RSP]
	FSQRT
	#IF ~LEGACY THEN
		FSTP QWORD [RSP]
		MOVSD XMM0, [RSP]
	#END
	POP RAX
#ELSE
	unimplemented
#END
END Sqrt;

PROCEDURE -Ln(x: Value):Value;
CODE
#IF I386 THEN
	FLD1
	FLDL2E
	FDIVP
	FLD QWORD [ESP]
	FYL2X
	#IF ~LEGACY THEN
		FSTP QWORD [ESP]
		MOVSD XMM0, [ESP]
	#END
	POP EAX
	POP EAX
#ELSIF AMD64 THEN
	FLD1
	FLDL2E
	FDIVP
	FLD QWORD [RSP]
	FYL2X
	#IF ~LEGACY THEN
		FSTP QWORD [RSP]
		MOVSD XMM0, [RSP]
	#END
	POP RAX
#ELSE
	unimplemented
#END
END Ln;

PROCEDURE -Exp(x: Value):Value;
CODE
#IF I386 THEN
	FLD QWORD [ESP]
	FLDL2E
	FMULP
	FLD ST0
	FRNDINT
	FXCH ST1
	FSUB ST0, ST1
	F2XM1
	FLD1
	FADDP
	FSCALE
	FSTP ST1
	#IF ~LEGACY THEN
		FSTP QWORD [ESP]
		MOVSD XMM0, [ESP]
	#END
	POP EAX
	POP EAX
#ELSIF AMD64 THEN
	FLD QWORD [RSP]
	FLDL2E
	FMULP
	FLD ST0
	FRNDINT
	FXCH ST1
	FSUB ST0, ST1
	F2XM1
	FLD1
	FADDP
	FSCALE
	FSTP ST1
	#IF ~LEGACY THEN
		FSTP QWORD [RSP]
		MOVSD XMM0, [RSP]
	#END
	POP RAX
#ELSE
	unimplemented
#END
END Exp;

PROCEDURE sin*(x: Value): Value;
BEGIN
	RETURN Sin(x);
END sin;

PROCEDURE cos*(x: Value): Value;
BEGIN
	RETURN Cos(x);
END cos;

PROCEDURE arctan*(x: Value): Value;
BEGIN
	RETURN Arctan(x)
END arctan;

PROCEDURE sqrt*(x: Value): Value;
BEGIN
	IF x <= 0 THEN
		IF x = 0 THEN RETURN 0 ELSE HALT(80) END
	ELSE
		RETURN Sqrt(x)
	END
END sqrt;

PROCEDURE ln*(x: Value): Value;
BEGIN
	IF x <= 0 THEN
		HALT(80)
	ELSE
		RETURN Ln(x)
	END
END ln;

PROCEDURE exp*(x: Value): Value;
BEGIN
	RETURN Exp(x)
END exp;

END MathL.
